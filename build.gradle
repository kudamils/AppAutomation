plugins {
    id 'java'
    id 'com.adarshr.test-logger' version '1.6.0'
    id 'com.novoda.static-analysis' version '1.0'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.6"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'io.rest-assured:rest-assured:4.0.0'
    testImplementation 'io.github.cdimascio:java-dotenv:5.1.1'
    testImplementation 'io.cucumber:cucumber-java8:4.7.1'
    testImplementation 'io.cucumber:cucumber-junit:4.7.1'
    testImplementation 'com.google.code.gson:gson:2.8.5'
}

clean.doFirst {
    delete "${rootDir}/rerun"
    delete "${rootDir}/target"
    delete "${rootDir}/out"
}

staticAnalysis {
    penalty {
        maxErrors = 0
        maxWarnings = 0
    }
    pmd {}
    findbugs {
        exclude '**/*Factory.java'
    }
}

testlogger {
    theme 'mocha'
    showExceptions true
    showPassedStandardStreams false
    showSkippedStandardStreams false
    showFailedStandardStreams true
}

test {
    systemProperties = System.properties
}

cucumberReports {
    outputDir = file('target/')
    buildId = '0'
    reports = files('target/cucumber-report.json')
}

task rerunCucumber() {
    doLast {
        javaexec {
            systemProperties = System.properties
            main = "cucumber.api.cli.Main"
            classpath = configurations.testRuntimeClasspath + sourceSets.main.output + sourceSets.test.output
            args = [
                    '@rerun/failed_scenarios.txt',
                    '--plugin', 'pretty',
                    '--plugin', 'json:target/cucumber-report.json',
                    '--plugin', 'rerun:rerun/failed_scenarios.txt',
                    '--glue', 'bukalapak'
            ]
        }
    }
}